PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rp: <http://www.marioverhaeg.nl/rp#>

SELECT DISTINCT ?c ?t 
WHERE 
{
	# Gather insights (opportunities and challenges)
	{
		?r rp:addresses ?c .
		?c rdf:type ?t
		FILTER(?t != owl:Thing &&
		?t != rp:Addressed_Insight &&
		?t != rp:Addressed_Opportunity &&
		?t != rp:Addressed_Challenge).
	}
	UNION
	# Gather visions
	{
		?r rp:addresses ?i .
		?i rp:has_information ?infa .
		?infa rdf:type rp:Insight_Vision_Contribution .
		?infa rp:based_on_information ?infb .
		?c rp:has_information ?infb .
		?c rdf:type ?t .	
		FILTER(?t != owl:Thing).				
	}
	FILTER(?r = rp:Requirement_SC4)
}